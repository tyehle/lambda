;; Base file defining most of the functions of the language

;; Useful Functions

(define (id x) x)
(define (const x _) x)
(define hang
  ((λ (u) (u u)) (λ (u) (u u))))


;; Values

(define (#t t f) t)
(define (#f t f) f)


;; Bool Ops

(define (if c t f)
  (c t f))
(define (and a b)
  (a b #f))
(define (or a b)
  (a #t b))
(define (not a)
  (a #f #t))


;; Numbers

(define (zero? n) (n (const #f) #t))
(define (succ n) (λ (f x) (f (n f x))))
(define (prev n)
  (λ (f x) (n (λ (g h) (h (g f)))
              (const x)
              id)))

(define (+ a b) (λ (f x) ((a f) (b f x))))
(define (* a b) (λ (f) (a (b f))))
(define (- a b) (b prev a))
(define (/ n m)
  (letrec (div1 (λ (n1 m1)
                  (let ([diff (- n1 m1)])
                    (if (zero? diff)
                        0
                        (succ (div1 diff m1))))))
    (div1 (succ n) m)))

(define (= a b) (and (zero? (- a b)) (zero? (- b a))))
(define (even? n) (n not #t))

(define (mod n m)
  (letrec (mod1 (λ (n1 m1)
                  (let ([diff (- n1 m1)])
                    (if (zero? diff)
                        n1
                        (mod1 diff m1)))))
    (prev (mod1 (succ n) m))))

(define (<= a b)
  (zero? (- a b)))
(define (>= a b)
  (zero? (- b a)))
(define (< a b)
  (<= (+ 1 a) b))
(define (> a b)
  (>= a (+ 1 b)))


;; Lists

(define (cons h t)
  (λ (f _) (f h t)))
(define empty
  (λ (_ e) e))
(define (head l)
  (l (λ (h _) h) hang))
(define (tail l)
  (l (λ (_ t) t) hang))
(define (pair? l)
  (l (λ (_ _) #t) #f))
(define (null? l)
  (l (λ (_ _) #f) #t))

(define (foldl fn acc xs)
  (if (null? xs)
      acc
      (foldl fn (fn acc (head xs)) (tail xs))))

(define (foldr fn acc xs)
  (if (null? xs)
      acc
      (fn (head xs) (foldr fn acc (tail xs)))))

(define (map f xs)
  (if (null? xs)
      xs
      (cons (f (head xs)) (map f (tail xs)))))

(define (take n l)
  (if (or (zero? n) (null? l))
      empty
      (cons (head l) (take (prev n) (tail l)))))

(define (from n)
  (cons n (from (succ n))))

(define (range low high)
  (if (>= low high)
      empty
      (cons low
            (range (+ 1 low)
                   high))))
